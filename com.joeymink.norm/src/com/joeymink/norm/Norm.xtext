grammar com.joeymink.norm.Norm with org.eclipse.xtext.xbase.Xbase

generate norm "http://www.joeymink.com/norm/Norm"
import "http://www.eclipse.org/xtext/xbase/Xbase"

NormFile:
	'normalization' name=QualifiedName '{'
	input=Input
	output=Output
	entities+=Entity*
	normalizations+=Normalization*
	'}'
;

// For input/output configuration

Output:
	'output' type=JvmTypeReference '{'
		properties+=IOProperty*
	'}'	
;

Input:
	'input' type=JvmTypeReference '{'
		properties+=IOProperty*
	'}'
;

IOProperty:
	key=ID '='value=STRING 
;

// Now the good stuff...

Entity:
	'entity' name=ID '{'
	attributes=Attributes?
	unique=Unique?
	'}'
;

Attributes:
	'attributes' '{'
	attributes+=Attribute+
	'}'
;

Attribute:
	name=ID reference=Reference?
;

Reference:
	'refs' attribute=[Entity]
;

Unique:
	'unique' (attributes+=[Attribute] ',')* attributes+=[Attribute]
;

Normalization:
	'norm' name=ID 'as' entity_type=[Entity] '{'
	mappings+=Mapping+
	'}'
;

Mapping:
	'map' attribute=[Attribute] 'as' (field=STRING | normalizedEntity=[Normalization])
;


